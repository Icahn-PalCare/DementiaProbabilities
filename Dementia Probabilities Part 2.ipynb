{
 "cells": [
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "## Project Name: Dementia Probabilities in HRS\n",
    "\n",
    "Project Synopsis: Follows Hurd's (2013) method of dementia probability\n",
    "assignment using separate ordered probits for self- and proxy-respondents on the\n",
    "ADAMS Wave A cohort as described in the online appendix.  Recreates tables from\n",
    "the appendix and examines sensitivity/specificity and AUC of model vs. Hurd's\n",
    "publicly available probabilities in manner of Gianattasio, et al. (2019).  Final\n",
    "probabilities are correlated with Hurd’s at .96, with slightly higher sensitivity\n",
    "and lower specificity, and marginally higher AUC in and out of sample than\n",
    "Hurd’s original probabilities.<br>\n",
    "\n",
    "Principal Investigator: Amy Kelley <br> <br>\n",
    "Created By: Evan Bollens-Lund<br>\n",
    "Date Created: 12/10/15<br>\n",
    "Last Modified By: Evan Bollens-Lund<br>\n",
    "Date Last Modified: 2/25/19<br>\n",
    "\n",
    "Data: HRS Core public files, 1998-2014\n",
    "Software: SAS and Stata\n",
    "\n",
    "Papers that use data derived from this code:\n",
    "Ornstein, K.A., Garrido, M.M., Siu, A.L., Bollens‐Lund, E., Langa, K.M. and Kelley, A.S., 2018. Impact of In‐Hospital Death on Spending for Bereaved Spouses. Health Services Research, 53, pp.2696-2717.\n",
    "\n",
    "Ornstein, K.A., Garrido, M.M., Siu, A.L., Bollens-Lund, E., Rahman, O.K. and Kelley, A.S., 2019. An Examination of Downstream Effects of Bereavement on Healthcare Utilization for Surviving Spouses in a National Sample of Older Adults. Forthcoming from PharmacoEconomics.\n",
    "\n",
    "Wachterman, M.W., O’Hare, A.M., Rahman,O.K., Lorenz, K.A., Marcantonio, E.R., Alicante, G.K. and Kelley, A.S., 2019. Framing Prognostic Expectations: One-year Mortality After Dialysis Initiation Among Older Adults.  Forthcoming from JAMA Internal Medicine.\n",
    "\n",
    "Using the original Hurd probabilities:\n",
    "Kelley, A.S., McGarry, K., Gorges, R. and Skinner, J.S., 2015. The burden of health care costs for patients with dementia in the last 5 years of life. Annals of internal medicine, 163(10), pp.729-736.\n",
    "\n",
    "Reference papers:\n",
    "Hurd, Michael D., Paco Martorell, Adeline Delavande, Kathleen J. Mullen,\n",
    " and Kenneth M. Langa. \"Monetary costs of dementia in the United States.\"\n",
    " New England Journal of Medicine 368, no. 14 (2013): 1326-1334.\n",
    "\n",
    "Gianattasio, Kan Z., Qiong Wu, M. Maria Glymour, and Melinda C. Power. \"\n",
    "Comparison of methods for algorithmic classification of dementia status\n",
    " in the Health and Retirement Study.\" Epidemiology 30, no. 2 (2019): 291-302."
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "## Stata\n",
    "\n",
    "Run this set of code after running the SAS code"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "use \"E:\\data\\serious_ill\\int_data\\proxy.dta\", clear\n",
    "\n",
    "\n",
    "local fvars 1389 1394 1399 1404 1409 1414 1419 1424 1429 1434 ///\n",
    " 1439 1444 1448 1451 1454 1457\n",
    "\n",
    "local i=506\n",
    "foreach f of local fvars {\n",
    "    gen fd`i'=f`f'\n",
    "    gen fd`=`i'+1'=f`=`f'+1'\n",
    "    gen fd`=`i'+2'=f`=`f'+2'\n",
    "    local i=`i'+3\n",
    "}\n",
    "\n",
    "local gvars 1543 1548 1553 1558 1563 1568 1573 1578 1583 1588 1593 ///\n",
    "1598 1602 1605 1608 1611\n",
    "\n",
    "local i=506\n",
    "foreach g of local gvars {\n",
    "    gen gd`i'=g`g'\n",
    "    gen gd`=`i'+1'=g`=`g'+1'\n",
    "    gen gd`=`i'+2'=g`=`g'+2'\n",
    "    local i=`i'+3\n",
    "}\n",
    "keep hhid pn *d* core_year\n",
    "tokenize f g h j k l m n o\n",
    "gen cy2=(core_year-1996)/2\n",
    "levelsof cy2, local(levels)\n",
    "\n",
    "local j=1\n",
    "local k=506\n",
    "forvalues i=1/16 {\n",
    "    gen base=.\n",
    "    gen bet=.\n",
    "    gen worse=.\n",
    "    gen pc`i'_notdone=0\n",
    "    foreach j of local levels {\n",
    "        qui replace pc`i'_notdone=1 if ``j''d`k'==4 & cy2==`j' \n",
    "        qui replace base=``j''d`k' if cy2==`j'\n",
    "        qui replace bet=``j''d`=`k'+1' if cy2==`j'\n",
    "        qui replace worse=``j''d`=`k'+2' if cy2==`j'\n",
    "}\n",
    "    gen pc`i'=3 if base==2\n",
    "    qui replace pc`i'=bet if inlist(bet,1,2)\n",
    "    qui replace pc`i'=worse if inlist(worse,4,5)\n",
    "    drop base bet worse\n",
    "    local k=`k'+3\n",
    "    local j=`j'+1\n",
    "}\n",
    "\n",
    "forvalues i=1/16 {\n",
    "    local pc `pc' pc`i'\n",
    "}\n",
    "foreach x in miss total mean {\n",
    "    egen iq`x'=row`x'(`pc')\n",
    "\n",
    "}\n",
    "\n",
    "\n",
    "\n",
    "\n",
    "\n",
    "drop if iqmiss==16\n",
    "save E:/data/serious_ill/int_data/proxycog.dta, replace\n",
    "\n",
    "\n",
    "\n",
    "H=\"get pdem for all and export dataset\"\n",
    "use \"E:\\data\\hrs_public_2014\\rand2014\\main\\trk2014tr_r.dta\", clear\n",
    "\n",
    "\n",
    "tokenize a b c d e f g h j k l m n o\n",
    "\n",
    "local yr=1\n",
    "foreach x in 1992 1993 1994 1995 1996 1998 2000 2002 2004 2006 2008 2010 2012 2014 {\n",
    "\trename ``yr''age age`x'\n",
    "\tlocal yr=`yr'+1\n",
    "}\t\n",
    "keep hhid pn age* gender\n",
    "reshape long age, i(hhid pn) j(core_year)\n",
    "tempfile track\n",
    "save `track'\n",
    "\n",
    "use \"E:\\data\\hrs_cleaned\\core_00_to_14.dta\", clear\n",
    "gen prediction_year=core_year+1\n",
    "merge 1:1 hhid pn core_year using \"E:\\data\\hrs_cleaned\\working\\tics_9814.dta\" , nogen keep(match master)\n",
    "merge 1:1 hhid pn prediction_year using  \"E:\\data\\hrs_public_2012\\dementia\\pdem_withvarnames.dta\", keepusing(prob)  keep(match master) nogen\n",
    "merge 1:1 hhid pn core_year using \"E:\\data\\hrs_public_2012\\dementia\\ADAMS\\dementia_dx_adams_wave1_only.dta\", nogen\n",
    "merge m:1 hhid  pn core_year using `track', keepusing(age gender) keep(match master) nogen\n",
    "merge 1:1 hhid pn core_year using \"E:\\data\\serious_ill\\int_data\\proxycog.dta\", nogen keep(match master) keepusing(iq*)\n",
    "\n",
    "replace female=gender-1 if !missing(gender)\n",
    "\n",
    "replace c_ivw_date=td(31dec2014) if core_year==2014 & missing(c_ivw_date)\n",
    "\n",
    "keep if age>=65\n",
    "gen age_cat=1\n",
    "replace age_cat=2 if age>=70\n",
    "replace age_cat=3 if age>=75\n",
    "replace age_cat=4 if age>=80\n",
    "replace age_cat=5 if age>=85\n",
    "replace age_cat=6 if age>=90\n",
    "tab age_cat, gen(age_cat)\n",
    "label define age_cat 1 \"Age<70\" 2 \"Age 70-74\" 3 \"Age 75-79\" 4 \"Age 80-84\" ///\n",
    "5 \"Age 85-89\" 6 \"Age>=90\"\n",
    "label values age_cat age_cat\n",
    "\n",
    "gen ed_hs_only=educ==2\n",
    "gen ed_gt_hs=educ>2\n",
    "gen n=1\n",
    "sort id core_year\n",
    "by id: gen hasn1=!missing(core_year[_n-1])\n",
    "egen adl_diff_index=rowtotal(adl_diff*)\n",
    "egen adlmiss=rowmiss(adl_diff*)\n",
    "local iadl iadl_diff_mp iadl_diff_gr iadl_diff_ph iadl_diff_rx iadl_diff_m \n",
    "sum n `iadl'\n",
    "egen iadl_diff_index=rowtotal(`iadl')\n",
    "egen iadlmiss=rowmiss(`iadl')\n",
    "replace adlmiss=1 if adlmiss>1\n",
    "replace iadlmiss=1 if iadlmiss>1\n",
    "*replace adl_diff_index=. if missing(adl_index_core)\n",
    "gen dates=mo+dy+yr+dw\n",
    "gen dates_imp=mo_imp==1 |dy_imp==1 | yr_imp==1 | dw_imp==1\n",
    "gen iqmissany=iqmiss>0 if !missing(iqmiss)\n",
    "gen iqmissgt2=iqmiss>2 if !missing(iqmiss)\n",
    "local cogvars proxy_core  imrc dlrc ser7 bwc20 dates scis cact pres vp adl_diff_index ///\n",
    "iadl_diff_index adlmiss iadlmiss iqmean iqmissgt2 iqmissany\n",
    "sum n `cogvars' if !proxy & !missing(dx_a)\n",
    "sum n `cogvars' if proxy & !missing(dx_a)\n",
    "\n",
    "\n",
    "foreach x of local cogvars {\n",
    "sort id core_year\n",
    "qui by id: gen prev`x'=`x'[_n-1]\n",
    "qui gen ch_`x'=`x'-prev`x'\n",
    "if inlist(\"`x'\",\"imrc\",\"dlrc\",\"ser7\",\"bwc20\",\"dates\",\"scis\",\"cact\",\"pres\",\"vp\") ///\n",
    "qui gen miss`x'=`x'_imp==1\n",
    "if !inlist(\"`x'\",\"imrc\",\"dlrc\",\"ser7\",\"bwc20\",\"dates\",\"scis\",\"cact\",\"pres\",\"vp\") ///\n",
    "qui gen miss`x'=`x'==.\n",
    "qui gen missprev`x'=prev`x'==.\n",
    "if inlist(\"`x'\",\"imrc\",\"dlrc\",\"ser7\",\"bwc20\",\"dates\",\"scis\",\"cact\",\"pres\",\"vp\") ///\n",
    "& proxy[_n-1]==0 qui replace missprev`x'=`x'_imp[_n-1]==1\n",
    "\n",
    "qui replace ch_`x'=0 if ch_`x'==.\n",
    "qui replace prev`x'=`x' if prev`x'==.\n",
    "sum prev`x' if proxy==1\n",
    "replace prev`x'=0 if missprev`x' & proxy==1 & prevproxy==1 & \"`x'\" !=\"proxy_core\"\n",
    "}\n",
    "gen adliadlmiss=adlmiss | iadlmiss\n",
    "egen cogmissany=rowmax(missbwc missser7 missscis misscact misspres missimrc missdlrc ///\n",
    "missdates adliadlmiss)\n",
    "\n",
    "egen missprev=rowmax(missprevimrc missprevdlrc missprevser7 missprevbwc20 missprevdates ///\n",
    "missprevscis missprevcact missprevpres missprevvp)\n",
    "\n",
    "/*note-most missing variables excluded due to collinearity*/\n",
    "local regvars  age_cat3 age_cat4 age_cat5 age_cat6 ed_hs_only ed_gt_hs female ///\n",
    "adl_diff_index iadl_diff_index ch_adl_diff ch_iadl_diff  dates bwc20 ///\n",
    "ser7 scis cact pres imrc dlrc ch_dates ch_bwc20 ch_ser7 ch_scis ch_cact ch_pres ///\n",
    "ch_imrc ch_dlrc \n",
    "\n",
    "local proxyvars  age_cat3 age_cat4 age_cat5 age_cat6 ed_hs_only ed_gt_hs female ///\n",
    "adl_diff_index iadl_diff_index ch_adl_diff ch_iadl_diff iqmean  ///\n",
    "prevproxy c.ch_iqmean prevdates prevser7 prevpres previmrc prevdlrc\n",
    "\n",
    "\n",
    "\n",
    "sum `regvars' if core_year==2012\n",
    "sum `regvars' if core_year==2014\n",
    "sum `regvars' if !proxy & !missing(dx_a)\n",
    "sum `proxyvars' if proxy & !missing(dx_a)\n",
    "\n",
    "\n",
    "oprobit dx `regvars' if proxy==0\n",
    "estimate store oprob1\n",
    "predict pself if proxy==0\n",
    "predict pself2 if proxy==0, outcome(#2)\n",
    "predict pself3 if proxy==0, outcome(#3)\n",
    "oprobit dx `proxyvars' if proxy==1\n",
    "estimates store oprob2\n",
    "predict pdem if proxy==1\n",
    "predict pdem2 if proxy==1, outcome(#2)\n",
    "predict pdem3 if proxy==1, outcome(#3)\n",
    "\n",
    "replace pdem=pself if proxy==0\n",
    "replace pdem2=pself2 if proxy==0\n",
    "replace pdem3=pself3 if proxy==0\n",
    "/*/note--this gets most-likely diagnosis, but we more commonly use a cutoff for \n",
    "probable dementia of pdem>.5*/\n",
    "gen ldem=1 if !missing(pdem)\n",
    "replace ldem=2 if pdem2>pdem\n",
    "replace ldem=3 if pdem3>pdem2 & pdem3>pdem\n",
    "gen likely_dem=ldem==1 if !missing(pdem)\n",
    "gen likely_cind=ldem==2 if !missing(pdem)\n",
    "gen likely_normal=ldem==3 if !missing(pdem)\n",
    "preserve\n",
    "keep id hhid pn proxy pdem* prob_dem core_year ldem \n",
    "rename prob_dem prob_hurd\n",
    "save E:\\data\\hrs_public_2014\\dementia\\pdem_withvarnames_00_14, replace\n",
    "restore\n",
    "gen dem=dx_adams==1 if !missing(dx_adams)\n",
    "*logit dem pdem\n",
    "*lroc\n",
    "gen mdem=missing(pdem)\n",
    "tab mdem\n",
    "gen mhurd=missing(prob) if core_year<=2006 & age>=70\n",
    "tab mdem mhurd if !missing(dx_a)\n",
    "\n",
    "sum `regvars' if !proxy & core_year>=2000\n",
    "sum `proxyvars' if proxy & core_year>=2000\n",
    "\n",
    "\n",
    "\n",
    "H=\"***********************************\"\n",
    "\n",
    "\n",
    "H=\"recreate hurd tables\"\n",
    "capture log close\n",
    "set more off\n",
    "\n",
    "cd \"E:\\data\\hrs_public_2014\\dementia\"\n",
    "\n",
    "\n",
    "use \"E:\\Files to move out\\New Data\\ucsf code for dementia in hrs\\cogvars_gdr_20170518.dta\", clear\n",
    "rename hhidpn id\n",
    "reshape long memimp dementpimp, i(id) j(wave)\n",
    "\n",
    "gen core_year=wave*2+1992\n",
    "tempfile wu\n",
    "save `wu'\n",
    "\n",
    "\n",
    "use  E:\\data\\hrs_public_2014\\dementia\\ADAMS\\dementia_dx_adams if !missing(dx), clear\n",
    "rename dx_a dx1\n",
    "gen dx_adams=dx1 if adams_wave==1\n",
    "rename core_year core_prior_to_adams\n",
    "gen core_year=core_prior if adams_wave==1\n",
    "replace core_year=2*(floor(adams_year/2)) if adams_wave>1\n",
    "replace core_year=2006 if adams_wave==3\n",
    "duplicates tag id core_year, gen(dup)\n",
    "replace core_year=core_year+2 if dup==1 & adams_wave>1\n",
    "merge 1:1 id core_year using E:\\data\\hrs_public_2014\\dementia\\pdem_withvarnames_00_14, nogen keep(match)\n",
    "merge m:1 hhid pn  using \"E:\\data\\hrs_public_2014\\rand2014\\main\\trk2014tr_r.dta\", ///\n",
    "nogen keep(match) keepusing(gender)\n",
    "\n",
    "gen female=gender-1\n",
    "\n",
    "gen stratum=.\n",
    "forvalues i=0/9 {\n",
    "replace stratum =`=`i'+1' if pdem>0.`i' & pdem<=0`=.`i'+.1'\n",
    "}\n",
    "\n",
    "xtile bin=pdem if adams_wave==1, nq(10)\n",
    "mat tab=J(11,6,.)\n",
    "local r=1\n",
    "local c=1\n",
    "forvalues i=1/10 {\n",
    "    mat tab[`r',1]=`i'\n",
    "    sum pdem if bin==`i'\n",
    "    mat tab[`r',2]=r(N)\n",
    "    mat tab[`r',3]=r(mean)\n",
    "    mat tab[`r',5]=r(mean)*r(N)\n",
    "    sum deme if bin==`i'\n",
    "    mat tab[`r',4]=r(mean)\n",
    "    mat tab[`r',6]=r(mean)*r(N)\n",
    "    local r=`r'+1\n",
    "}\n",
    "foreach i in 0 {\n",
    "    mat tab[`r',1]=`i'\n",
    "    sum pdem if !missing(dx_a)\n",
    "    mat tab[`r',2]=r(N)\n",
    "    mat tab[`r',3]=r(mean)\n",
    "    mat tab[`r',5]=r(mean)*r(N)\n",
    "    sum deme if !missing(dx_a) & !missing(pdem)\n",
    "    mat tab[`r',4]=r(mean)\n",
    "    mat tab[`r',6]=r(mean)*r(N)\n",
    "    local r=`r'+1\n",
    "}\n",
    "\n",
    "frmttable using \"E:\\data\\hrs_public_2014\\dementia\\hurd_replication_tables`c(current_date)'.rtf\", replace statmat(tab) ctitles(\"Bin\" \"N\" \"Fitted Prob\" \"Actual prob\" ///\n",
    "\"Estimated cases\" \"Actual cases\") sdec(0,0,3,3,3,0) ///\n",
    "title(Replication Hurd Table S1)\n",
    "\n",
    "rename dx1 dx_inwave\n",
    "sort id adams_wave\n",
    "by id: egen lw=max(adams_wave)\n",
    "levelsof adams_wave, local(levels)\n",
    "foreach l of local levels {\n",
    "    by id: egen dx`l'=max(cond(adams_wave==`l',dx_inwave,.))\n",
    "}\n",
    "\n",
    "by id, sort: egen adx=max(dx_a)\n",
    "label values adx dx_adams\n",
    "\n",
    "\n",
    "preserve\n",
    "keep if adams_wave==3 & !missing(ldem) & !missing(prob_)\n",
    "\n",
    "tab adx dx1, row nofreq\n",
    "tab adx ldem, row nofreq\n",
    "\n",
    "mat tab=J(3,4,.)\n",
    "local r=1\n",
    "local c=2\n",
    "\n",
    "foreach x in 3 2 \"3,2\" {\n",
    "    sum adx if inlist(adx,`x')\n",
    "    mat tab[`r',1]=r(N)\n",
    "    local denom=r(N)\n",
    "    foreach y in 3 2 1 {\n",
    "        sum adx if dx_inwave==`y' & inlist(adx,`x')\n",
    "        mat tab[`r',`c']=(r(N)/`denom')*100\n",
    "        local c=`c'+1\n",
    "}\n",
    "    local r=`r'+1\n",
    "    local c=2\n",
    "}\n",
    "\n",
    "mat rownames tab=Normal CIND Total\n",
    "\n",
    "frmttable using \"E:\\data\\hrs_public_2014\\dementia\\hurd_replication_tables`c(current_date)'.rtf\", addtable statmat(tab) ctitles(\"\" \"\" \"Wave C\" \"\" \\\"Wave A\" \"N\" \"Normal\" \"CIND\" ///\n",
    "\"Demented\") title(Replication Hurd table S4) sdec(0,1,1,1)\n",
    "\n",
    "\n",
    "gen likely_normal=ldem==3 if !missing(ldem)\n",
    "gen likely_cind=ldem==2 if !missing(ldem)\n",
    "gen likely_dem=ldem==1 if !missing(ldem)\n",
    "mat tab=J(4,3,.)\n",
    "local r=1\n",
    "local c=1\n",
    "\n",
    "foreach x in normal cind dem {\n",
    "    foreach y in 3 2 1 \"1,2,3\" {\n",
    "        sum likely_`x' if inlist(dx3,`y') \n",
    "        mat tab[`r',`c']=r(mean)*100\n",
    "        local r=`r'+1\n",
    "}\n",
    "    local c=`c'+1\n",
    "    local r=1\n",
    "}\n",
    "\n",
    "\n",
    "mat rownames tab=Normal CIND Demented Total\n",
    "\n",
    "frmttable using \"E:\\data\\hrs_public_2014\\dementia\\hurd_replication_tables`c(current_date)'.rtf\", ///\n",
    "addtable statmat(tab) ctitles(\"Wave C\" \"\" \"Model\" \"\" \\\"\" \"Normal\" \"CIND\" ///\n",
    "\"Demented\") title(Replication Hurd table S5) sdec(1)\n",
    "\n",
    "restore\n",
    "\n",
    "        di \"Answers set to `x' if not applicable/not done\"\n",
    "        di \"IQCode missing if >`miss' unanswered questions\"\n",
    "\n",
    "\n",
    "        destring id, replace\n",
    "        merge 1:1 id core_year using `wu', keep(match master)\n",
    "\n",
    "        sort id adams_wave\n",
    "        qui gen init =adams_wave==1 & !missing(prob) & !missing(dementpimp)\n",
    "        qui by id, sort: egen fdem=min(cond(dementia==1,adams_wave,.))\n",
    "\n",
    "        gen followup=adams_wave>1 & !missing(prob) & adams_wave<=fdem & !missing(dementpimp)\n",
    "        qui gen mydem=pdem>.5 if !missing(pdem)\n",
    "\n",
    "        di \"Test sample\"\n",
    "        qui logit dementia pdem if init==1\n",
    "        lroc, nograph\n",
    "        roctab dementia mydem if init==1 , detail\n",
    "        di \"Validation sample\"\n",
    "        qui logit dementia pdem if followup==1\n",
    "        lroc, nograph\n",
    "        roctab dementia mydem if followup==1 , detail\n",
    "\n",
    "        di \"Direct comparison to Hurd in Adams Wave C\"\n",
    "        qui gen hdem=prob>.5 if !missing(prob) \n",
    "        roctab dementia mydem if adams_wave==3 & !missing(prob) , detail\n",
    "        roctab dementia hdem if adams_wave==3 & !missing(pdem), detail\n",
    "\n",
    "        di \"Follow-up comparison among proxies\"\n",
    "        roctab dementia mydem if proxy==1 & followup==1, detail\n",
    "        roctab dementia hdem if proxy==1 & followup==1, detail\n",
    "        di \"Follow-up comparison among self-interviews\"\n",
    "        roctab dementia mydem if proxy==0 & followup==1, detail\n",
    "        roctab dementia hdem if proxy==0 & followup==1, detail\n",
    "\n",
    "        di \"Follow-up comparison among women\"\n",
    "        roctab dementia mydem if female==1 & followup==1, detail\n",
    "        roctab dementia hdem if female==1 & followup==1, detail\n",
    "        di \"Follow-up comparison among men\"\n",
    "        roctab dementia mydem if female==0 & followup==1, detail\n",
    "        roctab dementia hdem if female==0 & followup==1, detail\n",
    "\n",
    "        di \"Follow-up comparison among all\"\n",
    "        roctab dementia hdem if followup==1, detail\n",
    "        roctab dementia mydem if followup==1, detail\n"
   ]
  }
 ],
 "metadata": {
  "kernelspec": {
   "display_name": "Stata",
   "language": "stata",
   "name": "stata"
  },
  "language_info": {
   "codemirror_mode": "stata",
   "file_extension": ".do",
   "mimetype": "text/x-stata",
   "name": "stata",
   "version": "15.1"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 2
}
